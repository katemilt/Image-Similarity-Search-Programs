% CSI2120 Project Part 4
% Kaitlyn Miltimore, 300067827

% dataset(DirectoryName)
% this is where the image dataset is located
dataset('C:\\Users\\Kaitlyn\\Desktop\\CSI2120 - Project\\CSI2120-Project\\Prolog\\imageDataset2_15_20\\'). 

% directory_textfiles(DirectoryName, ListOfTextfiles)
% produces the list of text files in a directory
directory_textfiles(D,Textfiles):- directory_files(D,Files), include(isTextFile, Files, Textfiles).
isTextFile(Filename):-string_concat(_,'.txt',Filename).

% read_hist_file(Filename,ListOfNumbers)
% reads a histogram file and produces a list of numbers (bin values)
read_hist_file(Filename,Numbers):- open(Filename,read,Stream),read_line_to_string(Stream,_),
                                   read_line_to_string(Stream,String), close(Stream),
								   atomic_list_concat(List, ' ', String),atoms_numbers(List,Numbers).
								   
% similarity_search(QueryFile,SimilarImageList)
% returns the list of images similar to the query image
% similar images are specified as (ImageName, SimilarityScore)
% predicat dataset/1 provides the location of the image set
similarity_search(QueryFile,SimilarList) :- dataset(D), directory_textfiles(D,TxtFiles),
                                            similarity_search(QueryFile,D,TxtFiles,SimilarList).
											
% similarity_search(QueryFile, DatasetDirectory, HistoFileList, SimilarImageList)
similarity_search(QueryFile,DatasetDirectory, DatasetFiles,Best):- read_hist_file(QueryFile,QueryHisto), 
                                            compare_histograms(QueryHisto, DatasetDirectory, DatasetFiles, Scores), 
                                            sort(2,@>,Scores,Sorted),take(Sorted,5,Best).

% compare_histograms(QueryHisto,DatasetDirectory,DatasetFiles,Scores)
% compares a query histogram with a list of histogram files 
% produces a list of (HistogramFilename,Score) pairs
compare_histograms(_, _, [], []). % base case
compare_histograms(QueryHisto, DatasetDirectory, [HistoFileName|Files], [(HistoFileName, Score)|Pairs]) :-
    % get the filepath for the first histo, read the histo, calc intersection, compare w query
    atomic_list_concat([DatasetDirectory, HistoFileName], '/', FilePath), 
    read_hist_file(FilePath, Histo),
    histogram_intersection(QueryHisto, Histo, Score),
    compare_histograms(QueryHisto, DatasetDirectory, Files, Pairs).

% histogram_normalization(Histogram, NormalizedHistogram)
% normalize a histogram such that the values of all bins sum to 1.0
histogram_normalization(H, NormH) :-
    sum_list(H, Sum),
    maplist(quotient(Sum), H, NormH).

% quotient(Divisor, Dividend, Quotient)
% helper for histogram_normalization to get quotient
quotient(Divisor, Dividend, Quotient) :-
    Quotient is Dividend / Divisor.

% histogram_intersection(Histogram1, Histogram2, Score)
% compute the intersection similarity score between two histograms
% Score is between 0.0 and 1.0 (1.0 for identical histograms)
histogram_intersection(H1, H2, Score) :-
    % normalize the histos first so the intersection vals aren't crazy
    histogram_normalization(H1, NormH1),
    histogram_normalization(H2, NormH2),
    intersection(NormH1, NormH2, Intersection),
    Score is Intersection.

% intersection(List1, List2, Intersection)
% calculates the intersection values of corresponding bins in two histograms
% intersection can be computed as: sum over I(min(H1(I), H2(I))) (from project pt1)
intersection([], [], 0).
intersection([H1|T1], [H2|T2], Intersection) :-
    intersection(T1, T2, R),
    Min is min(H1, H2),
    Intersection is Min + R.

% take(List,K,KList)
% extracts the K first items in a list
take(Src,N,L) :- findall(E, (nth1(I,Src,E), I =< N), L).

% atoms_numbers(ListOfAtoms,ListOfNumbers)
% converts a list of atoms into a list of numbers
atoms_numbers([],[]).
atoms_numbers([X|L],[Y|T]):- atom_number(X,Y), atoms_numbers(L,T).
atoms_numbers([X|L],T):- \+atom_number(X,_), atoms_numbers(L,T).